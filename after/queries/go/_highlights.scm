; ;; extends
; ;; Keywords
; (("return"   @keyword) (#set! conceal "Û∞åë"))
; (("var"      @keyword) (#set! conceal  "ŒΩ"))
; (("if"       @keyword) (#set! conceal "? "))
; (("else"     @keyword) (#set! conceal "! "))
; (("func"     @keyword) (#set! conceal "ÔÇö"))
; (("for"      @keyword) (#set! conceal "ÔÄû"))
; (("switch"   @keyword) (#set! conceal "üéÆ"))
; (("default"  @keyword) (#set! conceal  "Ôò¨"))
; (("break"    @keyword.return) (#set! conceal  "‚èπÔ∏è"))
; (("case"     @keyword) (#set! conceal  "ÔÉ®"))
; (("import"   @keyword) (#set! conceal  "Ôüπ"))
; (("package"  @keyword) (#set! conceal  "Ôíá"))
; (("range"    @keyword) (#set! conceal "Ôõó"))
; (("chan"     @keyword) (#set! conceal "ÔÉ¨"))
; (("continue" @keyword) (#set! conceal "‚Üô"))
; (("struct"   @keyword) (#set! conceal "ÔÉå"))
; (("type"     @keyword) (#set! conceal "ÔÖ≥"))
; (("interface"       @keyword) (#set! conceal "Ôõ§"))
; (("*"       @keyword) (#set! conceal "‚ú≥Ô∏è"))
;
; ;; Function names
; ((call_expression function: (identifier) @function (#eq? @function "append"  )) (#set! conceal "Ôß´"))  ;; Ôôß
;
; ;; type
; (((type_identifier) @type (#eq? @type "string")) (#set! conceal "Óùø"))
; (((type_identifier) @type (#eq? @type "error")) (#set! conceal "ÔÅ±"))
; (((type_identifier) @type (#eq? @type "int")) (#set! conceal "Ô≠ç"))
; (((type_identifier) @type (#eq? @type "byte")) (#set! conceal "Ôë±"))
; (((nil) @type (#set! conceal "Ô≥†")))
; (((true) @type (#set! conceal "ÔêÆ")))
; ;; fmt.*
; (((selector_expression) @error (#eq? @error "fmt.Println"     )) (#set! conceal "ÔÄØ"))
; (((selector_expression) @error (#eq? @error "fmt.Printf"     )) (#set! conceal "Ô§´"))
; (((selector_expression) @field (#eq? @field "fmt.Sprintf"     )) (#set! conceal "Ôô•"))
; ;; type
; (((qualified_type) @type (#eq? @type "testing.T")) (#set! conceal "œÑ"))
; (((pointer_type) @type (#eq? @type "*testing.T")) (#set! conceal "œÑ"))
; ;; identifiers
; (((identifier) @type (#eq? @type "err"     )) (#set! conceal "Œµ"))
; (((identifier) @type (#eq? @type "errors"     )) (#set! conceal "√©"))
; (((identifier) @type (#eq? @type "request"     )) (#set! conceal "Ô¶â"))
; (((identifier) @type (#eq? @type "Request"     )) (#set! conceal "Ô¶â"))
; (((identifier) @type (#eq? @type "response"     )) (#set! conceal "ÔÑí"))
; (((identifier) @type (#eq? @type "Response"     )) (#set! conceal "ÔÑí"))
; (((identifier) @field (#eq? @field "Errorf"     )) (#set! conceal "ü•π"))
; (((identifier) @field (#eq? @field "assert"     )) (#set! conceal "üÖ∞Ô∏è"))
; (((field_identifier) @field (#eq? @field "Error"     )) (#set! conceal "E"))
; (((field_identifier) @field (#eq? @field "Equal"     )) (#set! conceal "üü∞"))
; (((identifier) @field (#eq? @field "fmt"     )) (#set! conceal "Ôùü"))
; extends

; Forked from tree-sitter-go
; Copyright (c) 2014 Max Brunsfeld (The MIT License)
;
; Identifiers
(type_identifier) @type

(type_spec
  name: (type_identifier) @type.definition)

(field_identifier) @property

(identifier) @variable

(package_identifier) @namespace

(parameter_declaration
  (identifier) @parameter)

(variadic_parameter_declaration
  (identifier) @parameter)

(label_name) @label

((identifier) @constant
  (#eq? @constant "_"))

(const_spec
  name: (identifier) @constant)

; Function calls
(call_expression
  function: (identifier) @function.call)

(call_expression
  function: (selector_expression
    field: (field_identifier) @method.call))

; Function definitions
(function_declaration
  name: (identifier) @function)

(method_declaration
  name: (field_identifier) @method)

(method_spec
  name: (field_identifier) @method)

; Operators
[
  "--"
  "-"
  "-="
  ":="
  "!"
  "!="
  "..."
  "*"
  "*"
  "*="
  "/"
  "/="
  "&"
  "&&"
  "&="
  "%"
  "%="
  "^"
  "^="
  "+"
  "++"
  "+="
  "<-"
  "<"
  "<<"
  "<<="
  "<="
  "="
  "=="
  ">"
  ">="
  ">>"
  ">>="
  "|"
  "|="
  "||"
  "~"
] @operator

; Keywords
[
  "break"
  "chan"
  "const"
  "continue"
  "default"
  "defer"
  "go"
  "goto"
  "interface"
  "map"
  "range"
  "select"
  "struct"
  "type"
  "var"
  "fallthrough"
] @keyword

"func" @keyword.function

"return" @keyword.return

"for" @repeat

[
  "import"
  "package"
] @include

[
  "else"
  "case"
  "switch"
  "if"
] @conditional

; Builtin types
((type_identifier) @type.builtin
  (#any-of? @type.builtin
    "any" "bool" "byte" "complex128" "complex64" "error" "float32" "float64" "int" "int16" "int32"
    "int64" "int8" "rune" "string" "uint" "uint16" "uint32" "uint64" "uint8" "uintptr"))

; Builtin functions
((identifier) @function.builtin
  (#any-of? @function.builtin
    "append" "cap" "close" "complex" "copy" "delete" "imag" "len" "make" "new" "panic" "print"
    "println" "real" "recover"))

; Delimiters
"." @punctuation.delimiter

"," @punctuation.delimiter

":" @punctuation.delimiter

";" @punctuation.delimiter

"(" @punctuation.bracket

")" @punctuation.bracket

"{" @punctuation.bracket

"}" @punctuation.bracket

"[" @punctuation.bracket

"]" @punctuation.bracket

; Literals
(interpreted_string_literal) @string

(raw_string_literal) @string @spell

(rune_literal) @string

(escape_sequence) @string.escape

(int_literal) @number

(float_literal) @float

(imaginary_literal) @number

[
  (true)
  (false)
] @boolean

(nil) @constant.builtin

(keyed_element
  .
  (literal_element
    (identifier) @field))

(field_declaration
  name: (field_identifier) @field)

; Comments
(comment) @comment @spell

; Doc Comments
(source_file
  .
  (comment)+ @comment.documentation)

(source_file
  (comment)+ @comment.documentation
  .
  (const_declaration))

(source_file
  (comment)+ @comment.documentation
  .
  (function_declaration))

(source_file
  (comment)+ @comment.documentation
  .
  (type_declaration))

(source_file
  (comment)+ @comment.documentation
  .
  (var_declaration))

; Errors
(ERROR) @error

; Spell
((interpreted_string_literal) @spell
  (#not-has-parent? @spell import_spec))

; ===== CUSTOM =====
(function_declaration
  (identifier) @function_definition)
